% ---ХАНОЙСКАЯ БАШНЯ---
% Дано: 3 колышка и 4 диска
% Цель: за n ходов переместить все диски из левого крайнего
% колышка на правый. За один ход можно перемещать только один
% самый верхний диск колышка. Кроме того, диск нельзя переместить
% на колышек, на котором уже находится диск меньшего размера.

% Кол-во ходов
#const moves =15.
% Колышки
peg(a;b;c).
% Диски
disk (1..4).
% Начальное состояние
init_on (1..4 ,a).
% Конечное состояние
goal_on (1..4 ,c).

% Ровно одно перемещение диска D на некоторый стержень P 
%должно быть выполнено в каждый момент T времени
1 { move(D,P,T) : disk(D), peg(P) } 1 :- T = 1..moves.


move(D,T)          :- move(D,_,T).
%Определение расположения дисков в начальный момент времени
on(D,P,0)          :- init_on(D,P).
%Текущий диск D перемежается на нужный колышек P
on(D,P,T)          :- move(D,P,T).
%Местоположение диска D переноситя с T на T+1, если D не перемещается в T+1
on(D,P,T+1)        :- on(D,P,T), not move(D,T+1), T < moves.

% Блокирует диски, которые нельзя перемещать в текущий момент времени
blocked(D-1,P,T+1) :- on(D,P,T), T < moves.
blocked(D-1,P,T)   :- blocked(D,P,T), disk(D).

% Ограничивающие условия
:- move(D,P,T), blocked(D-1,P,T).
:- move(D,T), on(D,P,T-1), blocked(D,P,T).
:- not 1 { on(D,P,T) } 1, disk(D), T = 1..moves.
:- goal_on(D,P), not on(D,P,moves).


#show move/3.