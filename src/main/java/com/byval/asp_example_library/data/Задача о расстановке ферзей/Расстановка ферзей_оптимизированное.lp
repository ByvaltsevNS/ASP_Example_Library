% ---ЗАДАЧА О РАССТАНОВКЕ ФЕРЗЕЙ: ОПТИМИЗИРОВАННЫЙ ВАРИАНТ---
% Дано: шахматная доска размером NxN и N ферзей
% Цель: расставить ферзей на доске таким образом,
% чтобы они не били друг друга оптимизированной
% реализацией

%Размер доски и кол-во ферзей
#const n = 8.

row(1..n).
col(1..n).

 %Возможные расстановки ферзей 
{ queen(I,1..n) } == 1 :- I = 1..n.
{ queen(1..n,J) } == 1 :- J = 1..n.

%Возможные значения диагоналей
d1(I,J,I-J+n) :- col(I), row(J).
d2(I,J,I+J-1) :- col(I), row(J).

%Высчитываются диагонали, ставится условие, при котором гоорится, что
%на горизонтали, вертикали и двух диагоналях должен быть только один ферзь 
:- D = 1..n*2-1, not { queen(I,J) : d1(I,J,D) } 1.
:- D = 1..n*2-1, not { queen(I,J) : d2(I,J,D) } 1.


#show queen/2.