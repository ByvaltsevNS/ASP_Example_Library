% ---СУДОКУ 4X4---
% Дано: игровое поле размером 4x4
% Цель: заполнить свободные клетки цифрами от 1 до 4 так,
% чтобы каждая цифра встречалась бы ровно один раз в каждой
% строке, каждом столбце и в каждом малом квадрате 2×2.

% Начальные значения в клетках
x(1,4,2).
x(2,1,1).
x(3,3,3).
x(4,1,4).

%Возможные значения в клетках
val(1..4).

%Границы групп клеток
border(1;3).

%Проверяем группу клеток
1{ x(X,Y,N): val(X), val(Y), X1<=X, X<= X1+1, Y1<=Y, Y<=Y1+1}1:- val(N), border(X1), border(Y1).

%Проверка по строкам и столбцам
1{ x(X,Y,N): val(N)}1 :- val(X), val(Y).
1{ x(X,Y,N) : val(X)}1 :- val(N), val(Y).
1{ x(X,Y,N) : val(Y)}1 :- val(N), val(X).
#show x/3.